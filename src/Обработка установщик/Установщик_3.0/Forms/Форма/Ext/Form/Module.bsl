
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	Объект.ОбъектСсылка        = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ДополнительнаяОбработкаСсылка");
	Объект.ОбработкаПодключена = ЗначениеЗаполнено(Объект.ОбъектСсылка);
	
	Если Объект.ОбработкаПодключена Тогда
		ХранилищеНастроек = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОбъектСсылка, "ХранилищеНастроек");
		Настройки = ХранилищеНастроек.Получить();
		Если ТипЗнч(Настройки) = Тип("Структура") Тогда 
			Если Настройки.Свойство("Автообновление") Тогда 
				Объект.Автообновление = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Настройки, "Автообновление"); 
			КонецЕсли; 
			Если Настройки.Свойство("Токен") Тогда 
				Объект.Токен = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Настройки, "Токен"); 
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	ОпределитьТекущуюВерсию();	
	ЗаполнитьДоступныеВерсии();
	
	Элементы.Токен.Видимость = Объект.ИспользоватьТокен;

КонецПроцедуры 

&НаСервере
Процедура ОпределитьТекущуюВерсию()    
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ОбрОбъект.ОпределитьТекущуюВерсию();
	ЗначениеВРеквизитФормы(ОбрОбъект,"Объект")
		
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьДоступныеВерсии()
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ОбрОбъект.ЗаполнитьДоступныеВерсии();
	ЗначениеВРеквизитФормы(ОбрОбъект,"Объект");
	
	Элементы.ОбновитьУстановщик.Доступность = Объект.ДоступноОбновлениеУстановщика; 
	
	ДоступноОбновлениеПродукта = Ложь;
	Для Каждого Стр Из Объект.ВерсииПродукта Цикл  
		Если Стр.ДоступноОбновление Тогда 
			ДоступноОбновлениеПродукта = Истина;
		КонецЕсли;
	КонецЦикла;
	Элементы.ОбновитьРасширение.Доступность = ДоступноОбновлениеПродукта;    
	
	//описание  
	Если Объект.ТекущаяВерсияРасширения = "неопределено" ИЛИ НЕ ЗначениеЗаполнено(Объект.ТекущаяВерсияРасширения) Тогда 
		Объект.Описание = "Расширение не установлено"; 
	ИначеЕсли ДоступноОбновлениеПродукта И Объект.ДоступноОбновлениеУстановщика Тогда 
		Объект.Описание = "Доступно обновление расширения и установщика"; 
	ИначеЕсли ДоступноОбновлениеПродукта Тогда 
		Объект.Описание = "Доступно обновление расширения"; 
	ИначеЕсли Объект.ДоступноОбновлениеУстановщика Тогда 
		Объект.Описание = "Доступно обновление установщика";
	Иначе 
		Объект.Описание = "Обновление не требуется";
	КонецЕсли;
			
КонецПроцедуры
               
&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Объект.ОбработкаПодключена Тогда
		СохраняемаяСтруктура = Новый Структура;
		СохраняемаяСтруктура.Вставить("Автообновление",Объект.Автообновление);	
		СохраняемаяСтруктура.Вставить("Токен",Объект.Токен);
		СохранитьНастройкиФормы(Объект.ОбъектСсылка, СохраняемаяСтруктура);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормы(ОбъектСсылка, СохраняемоеЗначение)
	
	ДополнительнаяОбработкаОбъект = ОбъектСсылка.ПолучитьОбъект();
	ДополнительнаяОбработкаОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(СохраняемоеЗначение);
	ДополнительнаяОбработкаОбъект.Записать();
		
КонецПроцедуры

&НаКлиенте
Процедура ВерсииПродуктаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)   
	
	Если Поле.Имя = "ВерсииПродуктаСкачать" Тогда
		СкачатьВерсиюПродукта(Элементы.ВерсииПродукта.ТекущиеДанные.ВерсияПродукта);
	ИначеЕсли Поле.Имя = "ВерсииПродуктаУстановить" Тогда
		УстановитьВерсиюПродукта(Элементы.ВерсииПродукта.ТекущиеДанные.ВерсияПродукта); 		
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура СкачатьВерсиюПродукта(НомерВерсииПродукта)  
	
	АдресХранилища = СкачатьВерсиюПродуктаСервер(НомерВерсииПродукта);
	
	Если АдресХранилища = Неопределено Тогда	
		 Возврат;	
	КонецЕсли;  
	
	МассивФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);  
	Для Каждого Эл Из МассивФайлов Цикл
		ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
		ДиалогВыбораФайла.ПолноеИмяФайла = Эл.ИмяФайла;
		ДиалогВыбораФайла.Расширение = ПолучитьРасширениеФайла(Эл.ИмяФайла);  
		АдресДД = ПоместитьВоВременноеХранилище(Эл.ДвоичныеДанные);
		ОписаниеОповещения = Новый ОписаниеОповещения("Подключаемый_ПриЗакрытииВыбораФайла", ЭтотОбъект, АдресДД);
		ДиалогВыбораФайла.Показать(ОписаниеОповещения);
	КонецЦикла;
	
КонецПроцедуры  

&НаКлиенте
Процедура Подключаемый_ПриЗакрытииВыбораФайла(ВыбранныеФайлы, ДопПараметры) Экспорт
	
	Если НЕ ВыбранныеФайлы = Неопределено Тогда
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДопПараметры);
	    Попытка
			ДвоичныеДанные.Записать(ВыбранныеФайлы[0]);
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;  	
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Функция СкачатьВерсиюПродуктаСервер(НомерВерсииПродукта)
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбрОбъект.СкачатьВерсиюПродуктаСервер(НомерВерсииПродукта);
	
КонецФункции   

&НаКлиенте
Процедура УстановитьВерсиюПродукта(НомерВерсииПродукта)  
	
	АдресХранилища = СкачатьВерсиюПродуктаСервер(НомерВерсииПродукта);
	
	Если АдресХранилища = Неопределено Тогда	
		 Возврат;	
	КонецЕсли;  
	
	МассивФайлов = ПолучитьИзВременногоХранилища(АдресХранилища);  
	Для Каждого Эл Из МассивФайлов Цикл
		Расширение = ПолучитьРасширениеФайла(Эл.ИмяФайла);  
		АдресДД = ПоместитьВоВременноеХранилище(Эл.ДвоичныеДанные);
		Если НРег(Расширение) = "cfe" Тогда 
			УстановитьВерсиюПродуктаНаСервере(АдресДД);	
		КонецЕсли; 	
		ПоказатьОповещениеПользователя("Расширение успешно обновлено. Для применения новой версии необходимо перезапустить сеанс 1С",,,,СтатусОповещенияПользователя.Важное);
	КонецЦикла; 
	
	ОпределитьТекущуюВерсию(); 
	ЗаполнитьДоступныеВерсии();
	
КонецПроцедуры 

&НаСервере
Процедура УстановитьВерсиюПродуктаНаСервере(АдресХранилища)  
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ОбрОбъект.УстановитьВерсиюПродуктаНаСервере(АдресХранилища);
	 
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьУстановщик(Команда)   
	
	//после нажатия надо перезапустить установщик
	ПоказатьВопрос(Новый ОписаниеОповещения("ПослеОбновленияУстановщика",ЭтотОбъект),"После обновления установщика необходимо его перезапустить. Продолжить?",РежимДиалогаВопрос.ОКОтмена)

КонецПроцедуры 

&НаКлиенте
Процедура ПослеОбновленияУстановщика(Результат, Доппараметры) Экспорт   
	
	Если Результат = КодВозвратаДиалога.ОК Тогда 
		ОбновитьВерсиюУстановщикаСервер(Объект.ТекущаяВерсияУстановщика); 
		Закрыть();
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура ОбновитьВерсиюУстановщикаСервер(ВерсияУстановщика)
	
	ОбрОбъект = РеквизитФормыВЗначение("Объект");
	ОбрОбъект.ОбновитьВерсиюУстановщикаСервер(ВерсияУстановщика);

КонецПроцедуры   

&НаСервере
Функция ПолучитьРасширениеФайла(ИмяФайла)

	ПозицияПоследнейТочки = 0;
	РасширениеФайла = ИмяФайла;

	Пока Истина Цикл 
		ПозицияПоследнейТочки = Найти(РасширениеФайла, "."); 
		Если ПозицияПоследнейТочки = 0 Тогда   
			Прервать; 
		Иначе   
			РасширениеФайла = Сред(РасширениеФайла, ПозицияПоследнейТочки + 1)
		КонецЕсли;
	КонецЦикла;

	Возврат ?(РасширениеФайла = ИмяФайла, "", РасширениеФайла);

КонецФункции 


&НаКлиенте
Процедура ОбновитьРасширение(Команда)
	
	Для Каждого Стр ИЗ Объект.ВерсииПродукта Цикл
		Если Стр.ДоступноОбновление Тогда 
			УстановитьВерсиюПродукта(Стр.ВерсияПродукта);  
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры


